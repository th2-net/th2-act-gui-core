
plugins {
    id 'java-library'
    id 'com.google.protobuf' version '0.8.8'
    id "com.jfrog.artifactory" version '4.15.1'
}

apply plugin: "maven-publish"

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

group = 'com.exactpro.th2.act'
version = "${version_major}.${version_minor}-SNAPSHOT"

ext {
    sharedDir           = file("${project.rootDir}/shared")

    th2CommonVersion    = '1.1-SNAPSHOT'
    genDir = file("${projectDir}/build/generated")
    mainRsrcDir = 'src/main/resources'
}

repositories {
    maven {
        name 'MavenLocal'
        url sharedDir
    }
    mavenCentral()
    maven {
        name 'Artifactory snapshot'
        url 'http://artifactory5.exp.exactpro.com/artifactory/libs-snapshot'
    }
    mavenLocal()

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

sourceSets {
    main {
        java {
            srcDir "${genDir}/main/grpc"
            srcDir "${genDir}/main/java"
            srcDir "src/main/proto"
        }
        resources {
            srcDir "${mainRsrcDir}"
        }
    }
}

ext {
    slf4jVersion            = '1.7.30'
    grpcVersion             = '1.27.1'
    protobufVersion         = '3.11.0'
    sailfishVersion         = '3.2-SNAPSHOT'

    th2ConnectivityVersion  = '1.1-SNAPSHOT'
}

archivesBaseName = 'act-gui-template'


dependencies {
    compile "com.exactpro.th2:th2-common:${th2CommonVersion}"

    compile "com.exactpro.th2:th2-grpc-verifier:${th2ConnectivityVersion}"
    compile "com.exactpro.th2:th2-grpc-event-store:${th2ConnectivityVersion}"
    compile "com.exactpro.th2:th2-grpc-connectivity:${th2ConnectivityVersion}"

    compile "org.slf4j:slf4j-log4j12:${slf4jVersion}"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"

    testImplementation 'junit:junit:4.12'
}

jar {
    archivesBaseName = "${archivesBaseName}"
    manifest {
        attributes('Specification-Title': 'TH2 Act GUI template')
        attributes("Class-Path": configurations.compileClasspath.collect { "lib/" + it.getName() }.join(' '))
        attributes(
                'Created-By': "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                'Specification-Title': '',
                'Specification-Vendor': 'Exactpro Systems LLC',
                'Implementation-Title': project.archivesBaseName,
                'Implementation-Vendor': 'Exactpro Systems LLC',
                'Implementation-Vendor-Id': 'com.exactpro',
                'Implementation-Version': project.version
        )
    }
}

protobuf {

    protoc {
        artifact = "com.google.protobuf:protoc:3.12.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.30.0'
        }
    }
    generateProtoTasks.generatedFilesBaseDir = genDir
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "localRepo"
            url = sharedDir
        }
    }
}

artifactory {
    publish {
        contextUrl = "${artifactory_url}"
        repository {
            repoKey = "${artifactory_deploy_repo_key}" // The Artifactory repository key to publish to
            username = "${artifactory_user}" // The publisher user name
            password = "${artifactory_password}" // The publisher password
        }
        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be
            // published to Artifactory.
            publications('mavenJava')
            publishArtifacts = true
            publishBuildInfo = true
            // Properties to be attached to the published artifacts.
            publishPom = true
        }
    }
}


compileJava.dependsOn.add('generateProto')

clean {
    delete genDir
}